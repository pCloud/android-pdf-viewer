buildscript {
    repositories {
        google()
        mavenCentral()
        maven { url 'https://jitpack.io' }
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:8.9.1'
    }
}

allprojects {

    ext.applyForPlugin = { String plugin, Action<Project> action ->
        applyForPlugins([plugin], action)
    }

    ext.applyForPlugins = { Iterable<String> plugins, Action<Project> action ->
        for (String plugin : plugins) {
            if (project.plugins.hasPlugin(plugin)) {
                action.call(project)
            } else {
                project.plugins.withId(plugin, {
                    action.call(project)
                })
            }
        }
    }

    repositories {
        google()
        mavenCentral()
        maven { url = 'https://jitpack.io' }
    }
}

subprojects {

    def kotlinVersion = "2.0.0"

    //noinspection ConfigurationAvoidance
    configurations.all {
        resolutionStrategy.eachDependency { details ->
            if (details.requested.group == 'org.jetbrains.kotlin') {
                if (details.requested.name.startsWith('kotlin-stdlib')) {
                    details.because "Enforce a single Kotlin version ($kotlinVersion)."
                    details.useVersion kotlinVersion
                }
            }
        }
    }

    applyForPlugin('java') {
        java {
            sourceCompatibility JavaVersion.toVersion(JavaVersion.VERSION_11)
            targetCompatibility JavaVersion.toVersion(JavaVersion.VERSION_11)

            toolchain {
                languageVersion = JavaVersion.toVersion(JavaVersion.VERSION_17)
            }
        }
    }
}
